SRBDF Revised by Chengyu Pu from Dr. Zhu Xiaolin (Zhu, X., Helmer E., Liu, D., Chen, J., Gao, F., and Lefsky M. A flexible spatiotemporal method for fusing satellite images with different resolutions. Remote Sensing of Environment,doi:10.1016/j.rse.2015.11.016)

Pro GetData,ImgData = ImgData,ns = ns,nl = nl,nb = nb,Data_Type = Data_Type,$
    FileName = FileName,Map_info = map_Info, Fid = Fid
    Filter = ['all file;*.*']
    Envi_Open_File,FileName,R_Fid = Fid
    Envi_File_Query,Fid,ns = ns,nl = nl,nb = nb,Data_Type = Data_Type
    map_info = envi_get_map_info(fid=Fid)
    dims = [-1,0,ns - 1 ,0,nl - 1]
    case Data_Type Of
        1:ImgData = BytArr(ns,nl,nb)    
        2:ImgData = IntArr(ns,nl,nb)   
        3:ImgData = LonArr(ns,nl,nb)    
        4:ImgData = FltArr(ns,nl,nb)    
        5:ImgData = DblArr(ns,nl,nb)    
        6:ImgData = COMPLEXARR(ns,nl,nb)
        9:ImgData = DCOMPLEXARR(ns,nl,nb)
        12:ImgData = UINTARR(ns,nl,nb)   
        13:ImgData = ULONARR(ns,nl,nb)   
        14:ImgData = LON64ARR(ns,nl,nb)   
        15:ImgData = ULON64ARR(ns,nl,nb)   
    EndCase
    For i = 0,nb-1 Do Begin
       Dt = Envi_Get_Data(Fid = Fid,dims = dims,pos=i)
       ImgData[*,*,i] = Dt[*,*]
    EndFor
End
Function P_OLS, ind_v,dep_v,k,low,high    
  common V_PUB, matrix
  common V_PUB2, y
  nb=n_elements(dep_v)
  x=reform(dep_v)
  varend=fltarr(k)
  yend=fltarr(k,nb)
  for ii=0,k-1 do begin
    varend[ii]=sqrt(total((ind_v[ii,*]-mean(ind_v[ii,*]))^2))
    yend[ii,*]=ind_v[ii,*]
  endfor
  var=sqrt(total((dep_v-mean(dep_v))^2))
  y=dep_v
  matrix=yend
  y=transpose(double(y))
  glow=fltarr(k)+low
  ghigh=fltarr(k)+high
  gintial=fltarr(k)+1.0/float(k)
  gbnd   = [glow, ghigh]
  Lbnd =[0,100]
  nobj   = 1
  g      = gintial
  Lcomp = 'HMBL11'
  nobj=0
  CONSTRAINED_MIN, g, gbnd, Lbnd, nobj, Lcomp, inform
  L =total((matrix ## g- y)^2)
  return,g
END
FUNCTION HMBL11, g
  common V_PUB
  common V_PUB2
  L=total((matrix ## g-y)^2)
  RETURN, L
END
pro  SRBDF
 t0=systime(1)                  
 w=10                 
 num_similar_pixel=20        
 min_class=3.0              
 max_class=4.0
 num_pure=100                 
 DN_min=-30.0                  
 DN_max=10.0
 scale_factor=20            
 block_size=20                
 background=-9999                
 background_band=1           
 temp_file='./temp/SRBDF'        
FileName1 = Dialog_PickFile(title = 'open the fine image of the first pair:')
GetData,ImgData=fine1_whole,FileName = FileName1,Fid=fid0
envi_file_query,fid0,ns=ns,nl=nl,nb=nb,dims=dims
map_info = envi_get_map_info(fid = fid0)
patch_long=block_size*scale_factor
orig_ns=ns
orig_nl=nl
n_ns=ceil(float(orig_ns)/patch_long)
n_nl=ceil(float(orig_nl)/patch_long)
  ind_patch1=intarr(4,n_ns*n_nl)           
  ind_patch=intarr(4,n_ns*n_nl)
  location=intarr(4,n_ns*n_nl)
  for i_ns=0,n_ns-1,1 do begin
    for i_nl=0,n_nl-1,1 do begin
        ind_patch1[0,n_ns*i_nl+i_ns]=i_ns*patch_long
        ind_patch[0,n_ns*i_nl+i_ns]=max([0,ind_patch1[0,n_ns*i_nl+i_ns]-scale_factor])
        location[0,n_ns*i_nl+i_ns]=ind_patch1[0,n_ns*i_nl+i_ns]-ind_patch[0,n_ns*i_nl+i_ns]
        ind_patch1[1,n_ns*i_nl+i_ns]=min([ns-1,(i_ns+1)*patch_long-1])
        ind_patch[1,n_ns*i_nl+i_ns]=min([ns-1,ind_patch1[1,n_ns*i_nl+i_ns]+scale_factor])
        location[1,n_ns*i_nl+i_ns]=ind_patch1[1,n_ns*i_nl+i_ns]-ind_patch1[0,n_ns*i_nl+i_ns]+location[0,n_ns*i_nl+i_ns]
        ind_patch1[2,n_ns*i_nl+i_ns]=i_nl*patch_long
        ind_patch[2,n_ns*i_nl+i_ns]=max([0,ind_patch1[2,n_ns*i_nl+i_ns]-scale_factor])
        location[2,n_ns*i_nl+i_ns]=ind_patch1[2,n_ns*i_nl+i_ns]-ind_patch[2,n_ns*i_nl+i_ns]
        ind_patch1[3,n_ns*i_nl+i_ns]=min([nl-1,(i_nl+1)*patch_long-1])
        ind_patch[3,n_ns*i_nl+i_ns]=min([nl-1,ind_patch1[3,n_ns*i_nl+i_ns]+scale_factor])
        location[3,n_ns*i_nl+i_ns]=ind_patch1[3,n_ns*i_nl+i_ns]-ind_patch1[2,n_ns*i_nl+i_ns]+location[2,n_ns*i_nl+i_ns]
    endfor
  endfor
tempoutname=temp_file+'\temp_F1'
pos=indgen(nb)
for isub=0,n_ns*n_nl-1,1 do begin
  dims=[-1,ind_patch[0,isub],ind_patch[1,isub],ind_patch[2,isub],ind_patch[3,isub]]
  envi_doit, 'resize_doit', fid=fid0, pos=pos, dims=dims, interp=0, rfact=[1,1], $
    out_name=tempoutname+strtrim(isub+1,1), r_fid=r_fid1
  envi_file_mng, id=r_fid1, /remove
endfor
FileName2 = Dialog_PickFile(title = 'open the coarse image of the first pair:')
envi_open_file,FileName2,r_fid=fid
tempoutname=temp_file+'\temp_C1'
pos=indgen(nb)
for isub=0,n_ns*n_nl-1,1 do begin
  dims=[-1,ind_patch[0,isub],ind_patch[1,isub],ind_patch[2,isub],ind_patch[3,isub]]
  envi_doit, 'resize_doit', fid=fid, pos=pos, dims=dims, interp=0, rfact=[1,1], $
    out_name=tempoutname+strtrim(isub+1,1), r_fid=r_fid1
  envi_file_mng, id=r_fid1, /remove
endfor
envi_file_mng, id=fid, /remove
FileName5 = Dialog_PickFile(title = 'open the coarse image of the prediction time:')
envi_open_file,FileName5,r_fid=fid
tempoutname=temp_file+'\temp_C0'
pos=indgen(nb)
for isub=0,n_ns*n_nl-1,1 do begin
  dims=[-1,ind_patch[0,isub],ind_patch[1,isub],ind_patch[2,isub],ind_patch[3,isub]]
  envi_doit, 'resize_doit', fid=fid, pos=pos, dims=dims, interp=0, rfact=[1,1], $
    out_name=tempoutname+strtrim(isub+1,1), r_fid=r_fid1
  envi_file_mng, id=r_fid1, /remove
endfor
envi_file_mng, id=fid, /remove
background_whole=bytarr(ns,nl)
ind_back=where(fine1_whole[*,*,background_band-1] eq background, num_back)
if (num_back gt 0) then begin
   background_whole[ind_back]=1
  for iband=0, nb-1, 1 do begin 
    temp=fine1_whole[*,*,iband]
    temp[ind_back]=mean(temp[where(background_whole eq 0)])
    fine1_whole[*,*,iband]=temp
  endfor   
endif
tempoutname11=temp_file+'\fine1_nobackground'
Envi_Write_Envi_File,fine1_whole,Out_Name = tempoutname11
ind_back=0;clear this variable
temp=0
fine1_whole=0 
background_whole=0
envi_open_file,tempoutname11,r_fid=fid00
CHANGE_THRESH = .05
NUM_CLASSES = max_class
ITERATIONS = 20
ISO_MERGE_DIST = 0.05*DN_max
ISO_MERGE_PAIRS = 2
ISO_MIN_PIXELS = 200
ISO_SPLIT_SMULT = 1
ISO_SPLIT_STD = 0.05*DN_max
MIN_CLASSES = min_class
out_bname = 'IsoData'
out_name=temp_file+'\class_ISODATA'
ENVI_File_Query, fid00, DIMS=dims, NB=nb1
ENVI_DOIT, 'class_doit', fid=fid00, pos=indgen(nb1), dims=dims, $
  out_bname=out_bname, out_name=out_name, method=4, $
  r_fid=r_fid, $
  NUM_CLASSES = NUM_CLASSES, $
  ITERATIONS = ITERATIONS, $
  CHANGE_THRESH = CHANGE_THRESH, $
  ISO_MERGE_DIST = ISO_MERGE_DIST, $
  ISO_MERGE_PAIRS = ISO_MERGE_PAIRS, $
  ISO_MIN_PIXELS = ISO_MIN_PIXELS, $
  ISO_SPLIT_SMULT = ISO_SPLIT_SMULT, $
  ISO_SPLIT_STD = ISO_SPLIT_STD, $
  MIN_CLASSES = MIN_CLASSES
envi_open_file,out_name,r_fid=fid
envi_file_query, fid, dims=dims
tempoutname=temp_file+'\class'
pos=indgen(1)
for isub=0,n_ns*n_nl-1,1 do begin
  dims=[-1,ind_patch[0,isub],ind_patch[1,isub],ind_patch[2,isub],ind_patch[3,isub]]
  envi_doit, 'resize_doit', fid=fid, pos=pos, dims=dims, interp=0, rfact=[1,1], $
    out_name=tempoutname+strtrim(isub+1,1), r_fid=r_fid1
  envi_file_mng, id=r_fid1, /remove
endfor
envi_file_mng, id=fid, /remove,/delete
envi_file_mng, id=fid00, /remove,/delete
envi_file_mng, id=fid0, /remove
print,'there are total',n_ns*n_nl,' blocks'
for isub=0,n_ns*n_nl-1,1 do begin
  FileName = temp_file+'\temp_F1'
  GetData,ImgData=fine1,ns = ns,nl = nl,nb = nb,Data_Type = Data_Type,FileName = FileName+strtrim(isub+1,1),Fid = Fid11
  fine1=float(fine1)
  FileName = temp_file+'\temp_C1'
  GetData,ImgData=coarse1,FileName = FileName+strtrim(isub+1,1),Fid = Fid12
  coarse1=FLOAT(coarse1)
  FileName = temp_file+'\temp_C0'
  GetData,ImgData=coarse2,FileName = FileName+strtrim(isub+1,1),Fid = Fid13
  coarse2=FLOAT(coarse2)
  FileName = temp_file+'\class'
  GetData,ImgData=L1_class0,FileName = FileName+strtrim(isub+1,1),Fid = Fid14
  num_class=max(L1_class0)
  i_new_c=0
  L1_class=intarr(ns,nl)
  for iclass=0, num_class-1,1 do begin
    ind_ic=where(L1_class0 eq iclass+1 and fine1[*,*, background_band-1] ne background, num_ic)
    if (num_ic gt 0) then begin
      L1_class[ind_ic]=i_new_c+1
      i_new_c=i_new_c+1
    endif
  endfor
  num_class=max(L1_class)
if (num_class gt 0) then begin  
for ib=0,nb-1, 1 do begin
  sortIndex = Sort(fine1[*,*,ib])
  sortIndices = (Findgen(float(ns)*nl+1))/(float(ns)*nl)
  Percentiles=[0.0001, 0.9999]
  dataIndices = Value_Locate(sortIndices, Percentiles)
  data_1_4= (fine1[*,*,ib])[sortIndex[dataIndices]]
  ind_small=where(fine1[*,*,ib] le data_1_4[0] or fine1[*,*,ib] lt DN_min)
  temp=fine1[*,*,ib]
  temp[ind_small]=min((fine1[*,*,ib])[where(fine1[*,*,ib] gt data_1_4[0] and fine1[*,*,ib] ge DN_min)])
  fine1[*,*,ib]=temp
  ind_large=where(fine1[*,*,ib] ge data_1_4[1] or fine1[*,*,ib] gt DN_max)
  temp=fine1[*,*,ib]
  temp[ind_large]=max((fine1[*,*,ib])[where(fine1[*,*,ib] lt data_1_4[1] and fine1[*,*,ib] le DN_max)])
  fine1[*,*,ib]=temp
endfor
ii=0
ns_c=floor(ns/scale_factor)
nl_c=floor(nl/scale_factor)
index_f=intarr(ns,nl)
index_c=intarr(ns_c,nl_c)
for i=0, ns_c-1, 1 do begin
  for j=0,nl_c-1,1 do begin
    index_f[i*scale_factor:(i+1)*scale_factor-1, j*scale_factor:(j+1)*scale_factor-1]=ii
    index_c[i,j]=ii
    ii=ii+1.0
  endfor
endfor
  row_ind=intarr(ns,nl)
  col_ind=intarr(ns,nl)
  for i=0,ns-1,1 do begin
    col_ind[i,*]=i
  endfor
  for i=0,nl-1,1 do begin
    row_ind[*,i]=i
  endfor
  fine_c1=fltarr(ns_c,nl_c,nb)
  coarse_c1=fltarr(ns_c,nl_c,nb)
  coarse_c2=fltarr(ns_c,nl_c,nb)
  row_c=fltarr(ns_c,nl_c)
  col_c=fltarr(ns_c,nl_c) 
  for ic=0,ns_c-1, 1 do begin
    for jc=0,nl_c-1, 1 do begin
      ind_c=where(index_f eq index_c[ic,jc]) 
      row_c[ic,jc]= mean(row_ind[ind_c])
      col_c[ic,jc]= mean(col_ind[ind_c])    
      for ib=0,nb-1,1 do begin
        fine_c1[ic,jc,ib]=mean((fine1[*,*,ib])[ind_c])
        coarse_c1[ic,jc,ib]=mean((coarse1[*,*,ib])[ind_c])
        coarse_c2[ic,jc,ib]=mean((coarse2[*,*,ib])[ind_c])
      endfor
    endfor
  endfor
   Fraction1=fltarr(ns_c,nl_c,num_class)
   for ic=0,ns_c-1, 1 do begin
     for jc=0,nl_c-1, 1 do begin
       ind_c=where(index_f eq index_c[ic,jc], num_c)
       L1_class_c=L1_class[ind_c]
       for iclass=0, num_class-1,1 do begin
         ind_ic=where(L1_class_c eq iclass+1, num_ic)
         Fraction1[ic,jc,iclass]=float(num_ic)/float(num_c)         
       endfor
       if (total(Fraction1[ic,jc,*]) le 0.999) then begin  
           Fraction1[ic,jc,*]=0
       endif
     endfor
   endfor
het_index=fltarr(ns,nl)
scale_d=w
for i=0,ns-1, 1 do begin
  for j=0,nl-1, 1 do begin  
    ai=max([0,i-scale_d])       
    bi=min([ns-1,i+scale_d])
    aj=max([0,j-scale_d])
    bj=min([nl-1,j+scale_d])
    class_t=L1_class[i,j]
    ;select same-class pixels
    ind_same_class=where(L1_class[ai:bi,aj:bj] eq class_t, num_sameclass)
    het_index[i,j]=float(num_sameclass)/((bi-ai+1.0)*(bj-aj+1.0))
  endfor
endfor
c_rate=fltarr(num_class,nb)
min_allow=fltarr(nb)
max_allow=fltarr(nb)
for ib=0,nb-1,1 do begin
  min_allow[ib]=min(coarse_c2[*,*,ib]-coarse_c1[*,*,ib])-stddev(coarse_c2[*,*,ib]-coarse_c1[*,*,ib])
  max_allow[ib]=max(coarse_c2[*,*,ib]-coarse_c1[*,*,ib])+stddev(coarse_c2[*,*,ib]-coarse_c1[*,*,ib])
endfor
for ib=0,nb-1, 1 do begin
  X_matrix=fltarr(num_class,num_pure*num_class)
  y_matrix=fltarr(1,num_pure*num_class)
  ii=0
  for ic=1, num_class, 1 do begin
    order=sort(Fraction1[*,*,ic-1])
    order=reverse(order)
    ind_f=where(Fraction1[*,*,ic-1] gt 0.01, num_f) 
    num_pure1=min([num_f,num_pure])
    change_c=(coarse_c2[*,*,ib])[order[0:(num_pure1-1)]]-(coarse_c1[*,*,ib])[order[0:(num_pure1-1)]]        
    sortIndex = Sort(change_c)
    sortIndices = (Findgen(num_pure1+1))/float(num_pure1)
    Percentiles=[0.25, 0.75]
    dataIndices = Value_Locate(sortIndices, Percentiles)
    data_1_4= change_c[sortIndex[dataIndices]]   
    ind_nonchange=where(change_c ge data_1_4[0] and change_c le data_1_4[1],num_nonc)        
    y_matrix[0,ii:ii+num_nonc-1]=change_c[ind_nonchange]  
    for icc=0, num_class-1,1 do begin          
          f_c=(Fraction1[*,*,icc])[order[0:(num_pure1-1)]]      
          X_matrix[icc,ii:ii+num_nonc-1]=f_c[ind_nonchange]
    endfor
    ii=ii+num_nonc
  endfor
  X_matrix=X_matrix[*,0:ii-1]
  y_matrix=y_matrix[0,0:ii-1]
  result=P_OLS(X_matrix,y_matrix,num_class,min_allow[ib],max_allow[ib])  
  c_rate[*,ib]=result[*]
endfor
L2_1=fine1
for ic=1, num_class, 1 do begin
  ind_L1_class=where(L1_class eq ic)
  for ib=0,nb-1, 1 do begin
    temp=L2_1[*,*,ib]
    temp[ind_L1_class]=(fine1[*,*,ib])[ind_L1_class]+c_rate[ic-1,ib]
    L2_1[*,*,ib]=temp
  endfor
endfor
coarse_c2_p=fltarr(ns_c,nl_c,nb)
for ic=0,ns_c-1, 1 do begin
  for jc=0,nl_c-1, 1 do begin
    ind_c=where(index_f eq index_c[ic,jc])
    for ib=0,nb-1,1 do begin
      coarse_c2_p[ic,jc,ib]=mean((L2_1[*,*,ib])[ind_c])      
    endfor
  endfor
endfor
min_allow=fltarr(nb)
max_allow=fltarr(nb)
for ib=0,nb-1,1 do begin
  min_allow0=min([min(coarse2[*,*,ib]),min(L2_1[*,*,ib])])
  min_allow[ib]=max([min_allow0,DN_min])
  max_allow0=max([max(coarse2[*,*,ib]),max(L2_1[*,*,ib])])
  max_allow[ib]=min([max_allow0,DN_max])
endfor
L2_tps=fltarr(ns,nl,nb)
for ib=0,nb-1,1 do begin
  L2_tps[*,*,ib] = MIN_CURVE_SURF(coarse_c2[*,*,ib], col_c, row_c,/TPS, XPOUT=col_ind,  YPOUT=row_ind)
endfor
print,'finish TPS prediction'
predict_change_c=coarse_c2_p-fine_c1    
real_change_c=coarse_c2-coarse_c1        
;change_R=coarse_c2-coarse_c2_p
change_R=real_change_c-predict_change_c
     change_21_c=fltarr(ns_c,nl_c,nb)
     change_21=fltarr(ns,nl,nb)
    for ic=0,ns_c-1, 1 do begin
       for jc=0,nl_c-1, 1 do begin
         ind_c=where(index_f eq index_c[ic,jc],num_ii)
           for ib=0,nb-1,1 do begin
              diff_change=change_R[ic,jc,ib]  
              w_change_tps=(L2_tps[*,*,ib])[ind_c]-(L2_1[*,*,ib])[ind_c]
             if (diff_change le 0) then begin                 
               ind_noc=where(w_change_tps gt 0, num_noc)
                if (num_noc gt 0) then begin
                   w_change_tps[ind_noc]=0
                endif
             endif else begin                             
                ind_noc=where(w_change_tps lt 0, num_noc)
                if (num_noc gt 0) then begin
                  w_change_tps[ind_noc]=0
                endif     
             endelse
             w_change_tps=abs(w_change_tps) 
             w_unform=fltarr(num_ii)    
             w_unform[*]=abs(diff_change)
             w_change=w_change_tps*het_index[ind_c]+w_unform*(1.0-het_index[ind_c])+0.000001 
             w_change=w_change/(mean(w_change)) ;nomalize weight
             ind_extrem=where(w_change gt 10, num_extrem)
             if (num_extrem gt 0) then begin
               w_change[ind_extrem]=mean(w_change)
             endif
             w_change=w_change/(mean(w_change))         
             temp=change_21[*,*,ib]
             temp[ind_c]=w_change*diff_change            
             change_21[*,*,ib]=temp
           endfor
       endfor
     endfor
     fine2_2=L2_1+change_21
     for ib=0,nb-1, 1 do begin
       temp=fine2_2[*,*,ib]
       ind_min=where(temp lt min_allow[ib], num_min)
       if (num_min gt 0) then begin
         temp[ind_min]=min_allow[ib]
       endif
       ind_max=where(temp gt max_allow[ib], num_max)
       if (num_max gt 0) then begin
         temp[ind_max]=max_allow[ib]
       endif
       fine2_2[*,*,ib]=temp
     endfor           
     change_21=fine2_2-fine1
 endif else begin
    change_21=fine1-fine1
 endelse
 change_21=change_21[location[0,isub]:location[1,isub],location[2,isub]:location[3,isub],*]
 change_21=float(change_21)
print,'finish change prediction step ',isub+1,' block'
tempoutname1=temp_file+'\temp_change'
Envi_Write_Envi_File,change_21,Out_Name = tempoutname1+strtrim(isub+1,1)
envi_file_mng, id=Fid11, /remove
envi_file_mng, id=Fid12, /remove
envi_file_mng, id=Fid13, /remove
envi_file_mng, id=Fid14, /remove
endfor
  mfid=intarr(n_ns*n_nl)
  mdims=intarr(5,n_ns*n_nl)
  mpos=intarr(nb,n_ns*n_nl)
  pos=indgen(nb)
  x0=intarr(n_ns*n_nl)
  y0=intarr(n_ns*n_nl)
  for isub=0,n_ns*n_nl-1,1 do begin
      envi_open_file, tempoutname1+strtrim(isub+1,1), r_fid= sub_fid
     if (sub_fid eq -1) then begin
       envi_batch_exit
       return
     endif
      envi_file_query,  sub_fid, ns=sub_ns, nl=sub_nl
      mfid[isub] = sub_fid
      mpos[*,isub] = indgen(nb)
      mdims[*,isub] = [-1,0, sub_ns-1,0, sub_nl-1]
      x0[isub]=ind_patch1[0,isub]
      y0[isub]=ind_patch1[2,isub]
  endfor
  xsize = orig_ns
  ysize = orig_nl
  pixel_size = [1.,1.]
  use_see_through = replicate(1L,n_ns*n_nl)
  see_through_val = replicate(0L,n_ns*n_nl)
    out_name=temp_file+'_change'
    envi_doit, 'mosaic_doit', fid=mfid, pos=mpos, $
    dims=mdims, out_name=out_name, xsize=xsize, $
    ysize=ysize, x0=x0, y0=y0, georef=0,MAP_INFO=map_info, $
    out_dt=4, pixel_size=pixel_size, $
    background=0, see_through_val=see_through_val, $
    use_see_through=use_see_through
    for i=0,n_ns*n_nl-1,1 do begin
      envi_file_mng, id=mfid[i], /remove, /delete
    endfor
FileName6 = out_name
envi_open_file,FileName6,r_fid=fid
tempoutname=temp_file+'\temp_change'
pos=indgen(nb)
for isub=0,n_ns*n_nl-1,1 do begin
  dims=[-1,ind_patch[0,isub],ind_patch[1,isub],ind_patch[2,isub],ind_patch[3,isub]]
  envi_doit, 'resize_doit', fid=fid, pos=pos, dims=dims, interp=0, rfact=[1,1], $
    out_name=tempoutname+strtrim(isub+1,1), r_fid=r_fid1
  envi_file_mng, id=r_fid1, /remove
endfor
envi_file_mng, id=fid, /remove,/delete
for isub=0,n_ns*n_nl-1,1 do begin
  FileName = temp_file+'\temp_F1'
  GetData,ImgData=fine1,ns = ns,nl = nl,nb = nb,Data_Type = Data_Type,FileName = FileName+strtrim(isub+1,1),Fid = Fid11
  fine1=float(fine1)
  FileName = temp_file+'\temp_C1'
  GetData,ImgData=coarse1,FileName = FileName+strtrim(isub+1,1),Fid = Fid12
  coarse1=FLOAT(coarse1)
  FileName = temp_file+'\temp_C0'
  GetData,ImgData=coarse2,FileName = FileName+strtrim(isub+1,1),Fid = Fid13
  coarse2=FLOAT(coarse2)
  FileName = temp_file+'\class'
  GetData,ImgData=L1_class,FileName = FileName+strtrim(isub+1,1),Fid = Fid14
  FileName = temp_file+'\temp_change'
  GetData,ImgData=change_21,FileName = FileName+strtrim(isub+1,1),Fid = Fid15
  change_21=FLOAT(change_21)
  ;place the blended result
  fine2=fine1[location[0,isub]:location[1,isub],location[2,isub]:location[3,isub],*]
D_D_all=((w-indgen(w*2+1)#(intarr(1,w*2+1)+1))^2+(w-(intarr(w*2+1)+1)#indgen(1,w*2+1))^2)^0.5
D_D_all=reform(D_D_all,(w*2+1)*(w*2+1))
similar_th=fltarr(nb)
for iband=0,nb-1,1 do begin
  similar_th[iband,0]=stddev(fine1[*,*,iband])*2.0/num_class   
endfor
for i=location[0,isub],location[1,isub],1 do begin           
  for j=location[2,isub],location[3,isub],1 do begin
    if (fine1[i,j,background_band-1] ne background  ) then begin   
      ai=max([0,i-w])       
      bi=min([ns-1,i+w])
      aj=max([0,j-w])
      bj=min([nl-1,j+w])
      ci=i-ai      
      cj=j-aj
      col_wind=indgen(bi-ai+1)#(intarr(1,bj-aj+1)+1)*1.0
      row_wind=(intarr(bi-ai+1)+1)#indgen(1,bj-aj+1)*1.0
      similar_cand=fltarr((bi-ai+1)*(bj-aj+1)) ;pleace the similarity measure between each pixel and the target pixel
      position_cand=intarr((bi-ai+1)*(bj-aj+1))+1  ;place the location of each similar pixel
        for iband=0,nb-1,1 do begin
           cand_band=intarr((bi-ai+1)*(bj-aj+1))          
           wind_fine=fine1[ai:bi,aj:bj,iband]       
           S_S=abs(wind_fine-wind_fine[ci,cj])
           similar_cand=similar_cand+S_S/(wind_fine[ci,cj]+0.00000001)
           ind_cand=where(S_S lt similar_th[iband])
           cand_band[ind_cand]=1
           position_cand=position_cand*cand_band
        endfor
        indcand=where(position_cand ne 0,number_cand0)  ;select similar pixel initially
        order_dis=sort(similar_cand[indcand])
        number_cand=min([number_cand0,num_similar_pixel])
        ind_same_class=indcand[order_dis[0:number_cand-1]]               
        if ((bi-ai+1)*(bj-aj+1) lt (w*2.0+1)*(w*2.0+1)) then begin   
            D_D_cand=((ci-col_wind[ind_same_class])^2+(cj-row_wind[ind_same_class])^2)^0.5+0.00001 
        endif else begin
            D_D_cand=D_D_all[ind_same_class]     
        endelse
       D_D_cand=(1.0+D_D_cand/w)*(similar_cand[ind_same_class]+1.0)
       C_D=1.0/D_D_cand
       weight=C_D/total(C_D)
      for iband=0,nb-1,1 do begin
        change_cand=(change_21[ai:bi,aj:bj,iband])[ind_same_class]
        fine2[i-location[0,isub],j-location[2,isub],iband]=fine1[i,j,iband]+total(weight*change_cand)
        if (fine2[i-location[0,isub],j-location[2,isub],iband] lt DN_min ) then begin ;correct abnomal prediction
          another_predict=max([DN_min, fine1[i,j,iband]+(coarse2[i,j,iband]-coarse1[i,j,iband])])
          fine2[i-location[0,isub],j-location[2,isub],iband]=min([DN_max,another_predict])
        endif
        if (fine2[i-location[0,isub],j-location[2,isub],iband] gt DN_max ) then begin ;correct abnomal prediction
          another_predict=min([DN_max, fine1[i,j,iband]+(coarse2[i,j,iband]-coarse1[i,j,iband])])
          fine2[i-location[0,isub],j-location[2,isub],iband]=max([DN_min,another_predict])
        endif
      endfor
    endif
  endfor
endfor
case Data_Type Of
  1:fine2 = Byte(fine2)    
  2:fine2 = FIX(fine2)     
  3:fine2 = LONG(fine2)    
  4:fine2 = FLOAT(fine2)   
  5:fine2 = DOUBLE(fine2)  
  6:fine2 = COMPLEX(fine2)
  9:fine2 = DCOMPLEX(fine2)
  12:fine2 = UINT(fine2)   
  13:fine2 = ULONG(fine2)   
  14:fine2 = LONG64(fine2)  
  15:fine2 = ULONG64(fine2)  
EndCase
print,'finish final prediction ',isub+1,' block'
tempoutname1=temp_file+'\temp_blended'
Envi_Write_Envi_File,fine2,Out_Name = tempoutname1+strtrim(isub+1,1)
envi_file_mng, id=Fid11, /remove, /delete
envi_file_mng, id=Fid12, /remove, /delete
envi_file_mng, id=Fid13, /remove, /delete
envi_file_mng, id=Fid14, /remove, /delete
envi_file_mng, id=Fid15, /remove, /delete
endfor
print, 'time used:', floor((systime(1)-t0)/3600), 'h',floor(((systime(1)-t0) mod 3600)/60),'m',(systime(1)-t0) mod 60,'s'
mfid=intarr(n_ns*n_nl)
mdims=intarr(5,n_ns*n_nl)
mpos=intarr(nb,n_ns*n_nl)
pos=indgen(nb)
x0=intarr(n_ns*n_nl)
y0=intarr(n_ns*n_nl)
for isub=0,n_ns*n_nl-1,1 do begin
  envi_open_file, tempoutname1+strtrim(isub+1,1), r_fid= sub_fid
  if (sub_fid eq -1) then begin
    envi_batch_exit
    return
  endif
  envi_file_query,  sub_fid, ns=sub_ns, nl=sub_nl
  mfid[isub] = sub_fid
  mpos[*,isub] = indgen(nb)
  mdims[*,isub] = [-1,0, sub_ns-1,0, sub_nl-1]
  x0[isub]=ind_patch1[0,isub]
  y0[isub]=ind_patch1[2,isub]
endfor
xsize = orig_ns
ysize = orig_nl
pixel_size = [1.,1.]
use_see_through = replicate(1L,n_ns*n_nl)
see_through_val = replicate(0L,n_ns*n_nl)
out_name=FileName5+'_SRBDF'
envi_doit, 'mosaic_doit', fid=mfid, pos=mpos, $
  dims=mdims, out_name=out_name, xsize=xsize, $
  ysize=ysize, x0=x0, y0=y0, georef=0,MAP_INFO=map_info, $
  out_dt=Data_Type, pixel_size=pixel_size, $
  background=0, see_through_val=see_through_val, $
  use_see_through=use_see_through
for i=0,n_ns*n_nl-1,1 do begin
  envi_file_mng, id=mfid[i], /remove, /delete
endfor
end
